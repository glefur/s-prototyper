grammar fr.obeo.dsl.SPrototyper with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

generate sPrototyper "http://www.obeo.fr/dsl/SPrototyper"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmtypes

SPrototyper:
	'proto' name=ID '{'
		('qualifier' qualifier=STRING)?
		'generate' targetURI=STRING
		viewpoints+=SPViewpoint*
	'}'
;

SPViewpoint:
	'viewpoint' name=ID  '{'
		('shortcut' shortcut=STRING)?
		('ext' extension=STRING)?
		(serviceClass+=JavaServiceClassReference)*
		representations+=SPRepresentation*	
	'}'
;

SPRepresentation:
	SPTable | SPDiagram
;

SPTable:
	'table' name=ID '{'
		('label' label=STRING)? 
		('title' title=STRING)? 
		usages+=MetamodelUsage+
		'root' root=[ecore::EClass | ID]
		elements+=TableElement+
		properties+=TableProperty+
	'}'
;

TableElement:
	 (recursive ?= 'recursive')? 'element' eClass=[ecore::EClass | ID] 'accessibleThrough' expression=SPExpression (creatable ?= 'creatable' ('via' createExpression=RequestOrCreateExpression)?)? ('{' 
		subElements +=TableElement+
	'}')?
;

TableProperty:
	'property' feature=STRING 
	('label' label=STRING)? 
	('expression' expression=SPExpression)?
;


SPDiagram:
	'diagram' name=ID '{'
		('label' label=STRING)? 
		('title' title=STRING)? 
		metamodels+=MetamodelUsage+
		'root' root=STRING
		elements+=DiagramElement+	
	'}'
;

DiagramElement:
	Container | Node
;


Container:
	(creatable ?= 'creatable')? (recursive ?= 'recursive')? (containerType=ContainerType)? 'container' name=ID 'for' eClass=STRING 'accessibleThrough' expression=SPExpression (style=ContainerStyleDefinition)? (
		'{'
			elements+=DiagramElement+
		'}'
	)?
;

ContainerStyleDefinition:
	'[' color=ContainerColorDefinition (',' label=LabelStyleDefinition)? (',' border=BorderStyleDefinition)? ']'
;


ContainerType:
	'freeform' | 'list'
;

ContainerColorDefinition:
	SolidColorDefinition | GradientColorDefinition
;

Node:
	(creatable ?= 'creatable')? 'node' name=ID 'for' eClass=STRING 'accessibleThrough' expression=SPExpression (style=NodeStyleDefinition)?
;

NodeStyleDefinition:
	'[' color=SolidColorDefinition (',' label=LabelStyleDefinition)? (',' border=BorderStyleDefinition)? ']'
;


SolidColorDefinition:
	'color' color=Color
;

GradientColorDefinition:
	'gradient from' from=Color 'to' to=Color
;

LabelStyleDefinition:
	'label' (expression=SPExpression)? color=SolidColorDefinition ('size' size=INT)? (bold?='bold')? (italic?='italic')? 
;

BorderStyleDefinition:
	'border ' color=SolidColorDefinition ('size' size=INT)?
;

SPExpression:
	RequestExpression | RequestOrCreateExpression 
;

RequestExpression:
	VarRef | FeatureRef
;

RequestOrCreateExpression:
	AcceleoExpression | ServiceRef
;

AcceleoExpression:
	'acc:' value=STRING
;

VarRef:
	'var:' value=STRING
;

FeatureRef:
	'feature:' value=ID
;

ServiceRef:
	'service:' value=ID
;

MetamodelUsage:
	'metamodel' usage=[ecore::EPackage|STRING]
;

JavaServiceClassReference:
	'use' javaClass=JvmTypeReference
;

Color:
	PreDefinedColorDefinition
;

PreDefinedColorDefinition:
	color=PreDefinedColor
;

enum PreDefinedColor:
	WHITE='white' 
	| BLACK='black' 
	| BLUE='blue' | LIGHT_BLUE='light blue' | DARK_BLUE='dark blue'  
	| CHOCOLATE='chocolate' | LIGHT_CHOCOLATE='light chocolate' | DARK_CHOCOLATE='dark chocolate' 
	| GRAY='gray' | LIGHT_GRAY='light gray' | DARK_GRAY='dark gray'
	| GREEN='green' | LIGHT_GREEN='light green' | DARK_GREEN='dark green'
	| ORANGE='orange' | LIGHT_ORANGE='light orange' | DARK_ORANGE='dark orange'
	| PURPLE='purple' | LIGHT_PURPLE='light purple' | DARK_PURPLE='dark purple'
	| RED='red' | LIGHT_RED='light red' | DARK_RED='dark red'
	| YELLOW='yellow' | LIGHT_YELLOW='light yellow' | DARK_YELLOW='dark yellow'
;

